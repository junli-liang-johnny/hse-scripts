PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX hwbp: <https://hse-oahwb-profile.adaptcentre.ie#>
PREFIX healthdcatap: <http://example.org/healthdcatap#>

SELECT 
?title
?provenanceLabel
			 (BOUND(?accrualPeriodicity) AS ?hasAccrualPeriodicity)
			 (BOUND(?identifier) AS ?hasIdentifier)
			 (BOUND(?disaggregation) AS ?hasDisaggregation)
			 (BOUND(?numeratorDataElement) AS ?hasNumeratorDataElement)
			 (BOUND(?numeratorSource) AS ?hasNumeratorSource)
			 (BOUND(?healthThemeDomain) AS ?hasHealthThemeDomain)
			 (BOUND(?healthThemeSubdomain) AS ?hasHealthThemeSubdomain)
WHERE {
	?indicator a dcat:Dataset ;
		dcterms:title ?title .
	OPTIONAL { ?indicator dcterms:accrualPeriodicity ?accrualPeriodicity . }
	OPTIONAL { ?indicator dcterms:identifier ?identifier . }
	OPTIONAL { ?indicator hwbp:disaggregation ?disaggregation . }
	OPTIONAL { ?indicator hwbp:numeratorDataElement ?numeratorDataElement . }
	OPTIONAL { ?indicator hwbp:numeratorSource ?numeratorSource . }
	OPTIONAL { ?indicator healthdcatap:healthTheme ?healthThemeDomain . 
						 ?healthThemeDomain a healthdcatap:Domain . }
	OPTIONAL { ?indicator healthdcatap:healthTheme ?healthThemeSubdomain . 
						 ?healthThemeSubdomain a healthdcatap:Subdomain . }

	OPTIONAL { 
		?indicator dcterms:provenance ?provenance .
		?provenance a dcterms:ProvenanceStatement ;
			rdfs:label ?provenanceLabel .
	}
}